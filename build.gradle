buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.1-SNAPSHOT'
    }
}

apply plugin: 'forge'

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "58.0.1"
group= "mods.immibis.redlogic" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "RedLogic"

repositories {
    maven {
        name = "shinonome"
        url = "http://maven.shinonome.ch/"
    }
}

dependencies {
    compile 'junit:junit:4.11'
    compile 'mods.immibis.core:ImmibisCore:58.0.0:deobf'
}

minecraft {
    version = "1.7.2-10.12.0.1047"
    assetDir = "eclipse/assets"
}
processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task apiJar(type: Jar) {
    from sourceSets.main.java
    classifier = 'api'
    include 'mods/immibis/redlogic/api/**'
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact deobfJar
            artifact apiJar
        }
    }
    repositories {
        maven {
            url "${config.maven.url}"
        }
    }
}
